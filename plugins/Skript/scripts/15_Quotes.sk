# Copyright Â© Filip 'FokaStudio' Raczkowski, 2020 - 2021

on message receive:
	set {_content} to content of event-message
	{_content} contains "https://discord.com/channels/", "https://canary.discord.com/channels/" or "https://ptb.discord.com/channels/"
	set {_util::*} to {_content} split at nl
	set {_content} to join {_util::*} with " "
	set {_content::*} to {_content} split at " "
	loop {_content::*}:
		if loop-value contains "https://discord.com/channels/", "https://canary.discord.com/channels/" or "https://ptb.discord.com/channels/":
			set {_content} to loop-value
			exit loop
	set {_url} to {_content}
	replace all "https://discord.com/channels/", "https://canary.discord.com/channels/" and "https://ptb.discord.com/channels/" with "" in {_content}
	set {_quote::*} to split {_content} at "/"
	discord id of event-guild is {_quote::1}
	retrieve message from id {_quote::3} in channel with id {_quote::2} and store it in {_msg}
	set {_em::*} to embeds of {_msg}
	set {_att::*} to attachments of {_msg}
	make embed:
		set {_author} to member author of {_msg}
		set thumbnail of embed to "https://cdn.discordapp.com/emojis/913502011293581342.png?size=1024"
		set {_extra} to ""
		if {_em::1} is set:
			set {_extra} to "%nl%:heavy_plus_sign: *Contains embed(s), posting them as well*"
		if {_att::1} is set:
			set {_extra} to "%nl%:heavy_plus_sign: *Contains attachments(s), posting them as well*"
		add field named "Here's what they said:" with value "%{_msg}%%{_extra}%" to embed
		add field named "Jump!" with value "Click [here](%{_url}%) to jump to the message" to embed
		set color of embed to skript color from rgb 0, 170, 255
		set footer of embed to "Requested by %discord name of event-member%##%tag of event-member%"
		set footer icon of embed to discord user avatar of event-member
		if {_author} is set:
			set author of embed to "Quote from %{_author}%:"
			set author icon of embed to discord user avatar of {_author}
		else:
			set author of embed to "Quote from an Unkown Author:" 
		set timestamp of embed to now
	reply with last embed
	loop {_em::*}:
		set embed color of loop-value to red
		reply with loop-value
	loop {_at::*}:
		reply with url of loop-value