#!! Effects

effect do nothing:
	trigger:
		stop

#!! Expressions

expression [my]sql:
	get:
		return {-sql}

expression Kirumi:
	return type: bot
	get:
		return bot named "Kirumi"

expression [Kirumi] current version:
	return type: string 
	get:
		return "8"

expression kirumi(-| )default:
	return type: string
	get:
		return "000000000000000000"

#!! Properties of guilds

guild property config file:
	return type: string
	get:
		return "plugins/DiscordData/server_data/%discord id of expr-1%.json"

guild property [Kirumi] [discord] prefix:
	get:
		return value "prefix" from "plugins/DiscordData/server_data/%discord id of expr-1%.json"
	set:
		set value "prefix" from "plugins/DiscordData/server_data/%discord id of expr-1%.json" to change value

guild property [Kirumi] [discord] secondary prefix:
	get:
		set {_mention} to mention tag of (self member of bot named "Kirumi" in expr-1)
		set {_pref} to "%{_mention}% "
		return {_pref}

guild property [Kirumi] [discord] tag[( |-)]prefix:
	get:
		return value "prefix" from "plugins/DiscordData/server_data/%discord id of expr-1%.json"
	set:
		set value "prefix" from "plugins/DiscordData/server_data/%discord id of expr-1%.json" to change value

guild property audit[( |-)]channel:
	return type: string
	get:
		return value "audit_channel" from "plugins/DiscordData/server_data/%discord id of expr-1%.json"
	set:
		set value "audit_channel" from "plugins/DiscordData/server_data/%discord id of expr-1%.json" to change value
	delete:
		set value "audit_channel" from "plugins/DiscordData/server_data/%discord id of expr-1%.json" to "000000000000000000"

guild property welcome[( |-)]channel:
	return type: string
	get:
		return value "welcome_channel" from "plugins/DiscordData/server_data/%discord id of expr-1%.json"
	set:
		set value "welcome_channel" from "plugins/DiscordData/server_data/%discord id of expr-1%.json" to change value
	delete:
		set value "welcome_channel" from "plugins/DiscordData/server_data/%discord id of expr-1%.json" to "000000000000000000"

guild property suggestion[[s](-| )]channel:
	return type: string
	get:
		return value "suggestion_channel" from "plugins/DiscordData/server_data/%discord id of expr-1%.json"
	set:
		set value "suggestion_channel" from "plugins/DiscordData/server_data/%discord id of expr-1%.json" to change value
	delete:
		set value "suggestion_channel" from "plugins/DiscordData/server_data/%discord id of expr-1%.json" to "000000000000000000"

plural expression:
	patterns:
		example [usage] of %discordcommand% in [guild] %guild%
		%discordcommand%'s example [usage] in [guild] %guild%
	return type: strings
	get:
		set {_ex::*} to values "%expr-1%" from "plugins/DiscordData/bot_data/examples.json"
		set {_pref} to value "prefix" from "plugins/DiscordData/server_data/%discord id of expr-2%.json"
		loop {_ex::*}:
			set {_} to loop-value
			replace all "{PREFIX}" in {_} with "%{_pref}%"
			set {_ex::%loop-index%} to {_}
		return {_ex::*}

#!! Functions

function AND(conditions: booleans) :: boolean:
	if {_conditions::*} contains false:
		return false
	return true
	
function OR(conditions: booleans) :: boolean:
	if {_conditions::*} contains true:
		return true
	return false
