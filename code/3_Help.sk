# Copyright Â© Filip 'FokaStudio' Raczkowski, 2020 - 2021

function helpMessage(c: string, u: user, g: guild) :: embedbuilder:
	set {_guild} to {_g}
	set {_g} to discord id of {_g}
	make embed:
		if {_c} = "none":
			set title of embed to "Help"
			set description of embed to "**I am Kirumi Tojo, the *Ultimate Maid*. Please let me know if you require my service.**%nl%Do `%Kirumi discord prefix of {_guild}%help <category>` to learn about other commands"
			set color of the embed to skript color from rgb 220, 220, 220
			add inline field named ":scroll: General" with value "General command category" to fields of embed
			add inline field named ":smile: Fun" with value "The land of endless joy" to fields of embed
			add inline field named ":tools: Moderation" with value "Moderation capabilities of mine" to fields of embed
			add inline field named ":frame_photo: Image" with value "Let me become your personal make-up artist" to fields of embed
#			add inline field named "Tags" with value "My quick responses if you would wish to ask me anything" to fields of embed
#			add inline field named "Leveling" with value "My leveling features" to fields of embed
			add inline field named "Minecraft" with value "Some commands related to this famous game..." to fields of embed
			add inline field named "Music" with value "<:beta1:906500514068447242><:beta2:906500513971961916> :new: I have found an unused radio in the closet, maybe it still works...?" to fields of embed
			set thumbnail of embed to "https://fokastudio.github.io/Kirumi-Tojo/assets/kirumi/kirumi_7.png"
		else if first index of "command" in {_c} = 1:
			set {_c::*} to {_c} split at "."
			loop all discord commands:
				if "%loop-value%" = "%{_c::2}%":
					add "**%Kirumi discord prefix of {_guild}%%loop-value%**%nl%" to {_embed::*}
					add "**%loop-value's description%**%nl%" to {_embed::*}
					add "<:tag:906504708305342484> `Category:` %loop-value's category in proper case%" to {_embed::*}
					add "<:command:906500514089418772> `Usage:` %loop-value's usage%" to {_embed::*}
					set {_aliases::*} to discord aliases of loop-value
					remove loop-value from {_aliases::&}
					set {_aliases} to join {_aliases::*} with ", "
					add "<:edit:906500513980350504> `Aliases:` %{_aliases}%" to {_embed::*}
					add "<:branch:906504708351459358> `Examples:` " to {_embed::*}
					set {_ex::*} to example usage of loop-value in {_guild}
					loop {_ex::*}:
						add "<:reply:906517020651171860> %loop-value-2%" to {_embed::*}
					set color of the embed to skript color from rgb 35, 150, 50
					set description of embed to join {_embed::*} with nl
					set title of embed to "Help for command '%{_c::2}%'"
					set thumbnail of embed to "https://fokastudio.github.io/FokaStudio/kirumi/kirumi/kirumi_7.png"
		else:
			loop all discord command:
				if discord command category of loop-value = {_c}:
					add "<:command:906500514089418772> **`%Kirumi discord prefix of {_guild}%%loop-value%`** - %description of loop-value%" to {_embed::*}
				if size of {_embed::*} != 0:
					set color of the embed to skript color from rgb 108, 193, 116
					set description of embed to join {_embed::*} with nl
					set title of embed to "Help for category '%{_c}%'"
					set thumbnail of embed to "https://fokastudio.github.io/FokaStudio/kirumi/kirumi/kirumi_10.png"
				else:
					set color of the embed to skript color from rgb 255, 80, 80
					set description of embed to "I have sadly failed to find anything related to the query **""%{_c}%""**"
					set title of embed to "I have failed to fulfil your request"
					set thumbnail of embed to "https://fokastudio.github.io/FokaStudio/kirumi/kirumi/kirumi_9.png"
		set author of the embed to "Kirumi Tojo"
		set author icon of the embed to discord avatar of self member of bot named "Kirumi" in {_guild}
		set timestamp of embed to now
		set footer of embed to "Requested by %discord name of {_u}%##%tag of {_u}%"
		set footer icon of embed to discord user avatar of {_u}
	return last embed

discord command help [<string>]:
	aliases: helpmepls, mayigethelp, heeyadmin
	category: general
	usage: %Kirumi discord prefix of event-guild%help [<category|command>]
	description: Basic help command. Use it anytime you want
	prefixes: %Kirumi discord prefix of event-guild%
	trigger:
		make new dropdown with id "helpMessage.%discord id of event-member%.%discord id of event-channel%":
			set min range of dropdown to 1
			set max range of dropdown to 1
			add new choice with value "help.general" with name "General" with desc "General command category" with reaction "scroll" to choices of dropdown
			add new choice with value "help.fun" with name "Fun" with desc "The land of endless joy" with reaction "smile" to choices of dropdown
#			add new choice with value "help.tags" with name "Tags" with desc "My quick responses if you would wish to ask me anything" with reaction "pencil" to choices of dropdown
			add new choice with value "help.moderation" with name "Moderation" with desc "Moderation capabilities of mine" with reaction "tools" to choices of dropdown
			add new choice with value "help.image" with name "Image" with desc "Let me become your personal make-up artist" with reaction "frame_photo" to choices of dropdown
#			add new choice with value "help.leveling" with name "Leveling" with desc "My leveling features" with reaction "sparkles" to choices of dropdown
			add new choice with value "help.minecraft" with name "Minecraft" with desc "Some commands related to this famous game..." with reaction "green_book" to choices of dropdown
			add new choice with value "help.music" with name "Music" with desc "(BETA)   I have found an unused radio in the closet, maybe it still works...?" with reaction "notes" to choices of dropdown
		set {_dropdown} to last dropdown
		make dropdown {_dropdown} run one time:
			defer the interaction
			set {_id::*} to event-dropdown split at "."
			if AND(check [{_id::2} = discord id of event-member], check [{_id::3} = discord id of event-channel]) = false:
				reply with personal ":x: **This is not your %Kirumi discord prefix of event-guild%help session!**"
				stop
			set {_choice::*} to event-choices
			set {_page::*} to {_choice::1} split at "."
			edit event-message to show helpMessage("%{_page::2}%", event-user, event-guild)
		if arg-1 is not set:
			reply to event-message with helpMessage("none", event-user, event-guild) with event-bot with {_dropdown}
		else:
			loop all discord commands:
				if "%loop-value%" = arg-1:
					reply to event-message with helpMessage("command.%arg-1%", event-user, event-guild) with event-bot
					stop
			reply to event-message with helpMessage(arg-1, event-user, event-guild) with event-bot