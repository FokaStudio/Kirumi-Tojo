# Copyright Â© Filip 'FokaStudio' Raczkowski, 2020 - 2021

discord command setup [<text>] [<channel>]:
	prefixes: %Kirumi discord prefix of event-guild%
	description: Manage different things across the server
	category: general
	permissions: manage server
	usage: %Kirumi discord prefix of event-guild%setup <entry> [<channel>] (will create a new one if not specified)
	trigger:
		set {_everyone} to public role of event-guild
		if arg-1 is not set:
			make embed:
				set title of embed to "Setup Help"
				set description of embed to "**This is the help box for the %Kirumi discord prefix of event-guild%setup**%nl%This command has **2 arguments**:%nl%:one: Type of the so-called *setup action* - it accepts entries such as ""audit"", ""lobby"" and ""suggestions""%nl%:two: *Channel* to assign the *setup action* to. Leave blank to create a new one (fully sets permissions, names and descriptions, even slowmodes on certain ones)"
				set author of the embed to "Kirumi Tojo"
				set author icon of embed to avatar of self member of bot named "Kirumi" in event-guild
				set embed color of the embed to orange
				add inline field named "Audit Logs" with value "`%Kirumi discord prefix of event-guild%setup audit`%nl%allows you to setup an audit channel. All specific logs go directly there" to fields of embed
				add inline field named "Suggestions" with value "`%Kirumi discord prefix of event-guild%setup suggestions`%nl%allows you to setup a suggestions channel. All messages posted there are instantly deleted and turned into a poll. Message authors can't vote for any option in their own polls" to fields of embed
				add inline field named "Lobby" with value "`%Kirumi discord prefix of event-guild%setup lobby`%nl%allows you to setup a lobby channel *(formerly known as `Welcome Channel`)*. When a member joins or leaves the server, a message will be sent there notifying everyone about it" to fields of embed
				set thumbnail of embed to "https://fokastudio.github.io/FokaStudio/kirumi/kirumi/kirumi_1.png"
				set footer of embed to "Requested by %discord name of event-member%##%tag of event-member%"
				set footer icon of embed to avatar of event-member
				set timestamp of embed to now
			reply with last embed
		else:
			if arg-1 = "audit" or "audit_channel":
				if arg-2 is set:
					set auditchannel of event-guild to discord id of arg-2
					deny permission (view channel) to role {_everyone} in channel arg-2
					reply with "**:trophy: %arg-2% is now a Audit Logs channel! It will log some useful info for you there**" with event-bot
				else:
					if auditchannel of event-guild is not kirumi-default:
						reply with ":x: **Audit Logs channel was already created, you can only assign a new one now!**" with event-bot
					else:
						set {_action} to new text channel action in event-guild
						set channel name of {_action} to "audit-logs"
						set topic of {_action} to "Logs for Staff Members"
						create {_action} and store it in {_a}
						deny permission (view channel) to role {_everyone} in channel {_a}
						set auditchannel of event-guild to discord id of {_a}
						reply with ":trophy: **Succesfully created a new Audit Logs channel!**" with event-bot
						wait 3 seconds
						Kirumi log "If you see this message, it means that you have configured the Audit Logs channel properly!" as "info" in event-guild
			else if arg-1 is "suggestions", "suggestion_channel" or "suggestions_channel":
				if arg-2 is set:
					set suggestion channel of event-guild to discord id of arg-2
					allow permission (message send) to role {_everyone} in channel arg-2
					allow permission (message add reaction) to role {_everyone} in channel arg-2
					reply with "**:trophy: %arg-2% is now a Suggestion channel! Every message posted there will be automatically deleted and replaced with a small poll!**" with event-bot
				else:
					if suggestion channel of event-guild is not kirumi-default:
						reply with ":x: **Suggestion channel was already created, you can only assign a new one now!**" with event-bot
					else:
						set {_action} to new text channel action in event-guild
						set channel name of {_action} to "suggestions"
						set topic of {_action} to "Any message written here will be deleted and turned into a poll instead" with event-bot
						set slowmode of {_action} to 20
						create {_action} and store it in {_s}
						allow permission (message send) to role {_everyone} in channel {_s}
						deny permission (message add reaction) to role {_everyone} in channel {_s}
						set suggestion channel of event-guild to discord id of {_s}
						reply with ":trophy: **Succesfully created a new Suggestion channel!**" with event-bot
			else if arg-1 is "welcome", "welcome_channel" or "lobby":
				if arg-2 is set:
					set welcome channel of event-guild to discord id of arg-2
					reply with "**:trophy: %arg-2% is now a Lobby channel! New joins and leaves will be announced here!**" with event-bot
				else:
					if welcome channel of event-guild is not kirumi-default:
						reply with ":x: **Lobby channel was already created, you can only assign a new one now!**" with event-bot
					else:
						set {_action} to new text channel action in event-guild
						set channel name of {_action} to "lobby"
						set topic of {_action} to "Newly joined members have their presence announced here%nl%Works also when they leave" with event-bot
						create {_action} and store it in {_s}
						set welcome channel of event-guild to discord id of {_s}
						reply with ":trophy: **Succesfully created a new Lobby channel!**" with event-bot
