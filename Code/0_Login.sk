# Copyright 2021  FokaStudio

# Coded by: FokaStudio
# Original by: <none>
# Addons required: DiSky
# Other plugins: <none>


import:
	org.bukkit.Bukkit

function AND(conditions: booleans) :: boolean:
	if {_conditions::*} contains false:
		return false
	return true
	
function OR(conditions: booleans) :: boolean:
	if {_conditions::*} contains true:
		return true
	return false

# Login and startup message
init:
	Bukkit.getServer().unloadWorld("world", false)
	make new discord bot:
		enable all default intent
		login to "REDACTED" with name "Kirumi"
	set online status of bot named "Kirumi" to do not disturb
	set presence of bot "Kirumi" to playing "Project: SMP | play.projectsmp.tk"

# Utility, returns a color for the embed based on input
function kirumiColor(s: string) :: color:
	switch {_s}:
		case "error" or "err":
			return dark red
		case "warn" or "warning":
			return yellow
		case "info":
			return light blue
		case "moderation" or "mod":
			return light red
		case "other" or "?":
			return lime

# Effect to log smth to #audit-logs
effect [Kirumi] log %string% as %string% in [guild] %guild%:
	trigger:
		if {db_SI.audit_channel::%discord id of expr-3%} is set:
			make embed:
				set author of the embed to "Project: SMP | %expr-2 in proper case%"
				set author icon of the embed to avatar of user with id "722065810033475616"
				set title of the embed to "%expr-2%"
				set description of the embed to "%expr-1%"
				set timestamp of the embed to now
				set footer of the embed to "Kirumi Tojo"
				set color of the embed to kirumiColor(expr-2)
			send last embed to channel with id {db_SI.audit_channel::%discord id of expr-3%}

effect do nothing:
	trigger:
		stop

# Force smth to log into the audit log channel
discord command log <text> <text>:
	permissions: manage server
	prefixes: !
	usage: !log <error|warn|info|moderation|other> <message>
	category: moderation
	description: <STAFF> Log a message into your guild's log channel (if it exists)
	trigger:
		Kirumi log arg-2 as arg-1 in guild event-guild

on text channel delete:
	if discord id of event-textchannel = {db_SI.audit_channel::%discord id of event-guild%}:
		delete {db_SI.audit_channel::%discord id of event-guild%}

# Basically making Kirumi say a desired line of yours
discord command announce <text>:
	permissions: manage server
	prefixes: !
	usage: !announce <message>
	category: moderation
	description: <STAFF> Make me help you with making announcements (accepts formatting codes that are accesible only to bots, such as [link](URL))
	trigger:
		delete discord entity event-message
		reply with arg-1

discord command prefix <text>:
	permissions: manage server
	prefixes: !, %{db_SI.prefix::%discord id of event-guild%}%
	description: <STAFF> Set my prefix for commands
	category: Utility
	usage: !prefix <new prefix>
	trigger:
		if arg-1 = {db_SI.prefix::%discord id of event-guild%}:
			reply with ":x: **My new prefix can't be my old prefix!**"
		else:
			set {db_SI.prefix::%discord id of event-guild%} to arg-1
			reply with ":white_check_mark: **Succesfully changed my prefix!**"

on bot join:
	if "%event-bot%" = "Kirumi":
		add 1 to {guild_count}
		set {db_SI.prefix::%discord id of event-guild%} to "!"
		set {db_SI.%discord id of event-guild%::expconfig::required::1} to 200
		set {db_SI.%discord id of event-guild%::expconfig::multiplier} to 1.5
		set {db_SI.%discord id of event-guild%::expconfig::chance} to 50
		set {db_SI.%discord id of event-guild%::expconfig::min} to 5
		set {db_SI.%discord id of event-guild%::expconfig::max} to 25

on bot leave:
	if "%event-bot%" = "Kirumi":
		remove 1 from {guild_count}
		clear {db_SI.%discord id of event-guild%::*}
		clear {tags::%discord id of event-guild%::*}
		clear {tag::%discord id of event-guild%::*}
		delete {db_SI.suggestion_channel::%discord id of event-guild%}
		delete {db_SI.prefix::%discord id of event-guild%}

discord command test:
	prefixes: !
	trigger:
		reply with "%{guild_count}% %{db_SI.prefix::%discord id of event-guild%}%"

on message receive:
	mentioned members in event-message = self member of bot "Kirumi" in event-guild
	length of content of event-message = length of mention tag of self member of bot "Kirumi" in event-guild
	reply to event-message with "Hello %mention tag of event-member%, have you called me?%nl%**My prefix here is %{db_SI.prefix::%discord id of event-guild%}%**%nl%Do __*%{db_SI.prefix::%discord id of event-guild%}%help*__ to get my list of commands%nl%If you would need *any support* with the Bot, join the Discord server of my Master - FokaStudio"